(*

We call U the set of all paths used directly in a file:

- 1. Any path occurring in the file is in U. For example, List.map occurring in
     the file will add both List.map and List to U.
- 2. All paths for definitions in the current file are in U. So if module M = …
     occurs in the file then M is in U.
- 3. All paths for things “defined” using include or open in the current file
     are in U. It is possible that all of these would end up in D anyway via
     other rules, but it's not entirely obvious so I've included this rule here
     just to make sure they do.
- Note that constructors or fields only used via type-based disambiguation are
  not in U.

We call D the domain of discourse:

- 1. The paths of all the predefined types that are intended for direct use by
     users, like int, are in D.
- 2. If a path is in U then it is also in D.
- 3. If a module path is in U then all the paths of its subcomponents are in D.
- 4. If a value path is in U and its value description was written by a user -
     as opposed to being inferred - then the paths used in that description are
     in D.
- 5. If a module path is in U and its module description was written by a user -
     as opposed to being inferred - then the paths used in that description are
     in D, excluding those paths that only appear inside of a sig…end.
- 6. If a type path is in U then any paths used in its equation or
     representation are in D.
- 7. If a constructor or record field is in U then any paths used in its type
     are in D.
- 8. If a module type path is in U then any paths used in its definition are in
     D, excluding those paths that only appear inside of a sig…end.
- 9. If a class path is in U and its class description was written by a user -
     as opposed to being inferred - then of any paths used in that description
     are in D.
- 10. If a class type path is in U then any paths used in its definition are in
      D.
- 11. If a path is in D and it includes another module path within it, then that
      module path is also in D.
- 12. If a module path m in D - note D not U - is a module alias with target n
      and another path p in D includes n within it, then the path obtained by
      substituting the m for n in p is also in D.
*)

open Shape.Sig_component_kind
open Discourse_types
let log_section = "discourse"
let { Logger.log } = Logger.for_section log_section

let get () = !g
let set v = g := v
let reset () = g := empty_discourse

let record_usages = Config.merlin

let add_initial_discourse () =
  let d = !g in
  g := { d with paths = Lid_trie.union (Predef.discourse ()) d.paths }

let pp_substs fmt substs =
  let pp_sep ppf () = Format.fprintf ppf ";@;" in
  let pp_v fmt (p, s) =
    Format.fprintf fmt "%a -> [%a]" Path.print p
      (Format.pp_print_list ~pp_sep Pprintast.longident)
      s
  in
  let substs =
    Path.Map.bindings substs |> List.map (fun (p, s) -> (p, Lid_set.elements s))
  in
  Format.pp_print_list ~pp_sep pp_v fmt substs

let fold_on_common_lid_and_path_segments ~init ~kind ~f (lid, path) =
  (* TODO : is it always true that paths prefixes are always Module ?*)
  let rec aux acc kind ((lid, path) : Longident.t * Path.t) =
    let acc = f acc kind (lid, path) in
    match (lid, path) with
    | Lident _, Pident _ -> acc
    | Ldot (l, _), Pdot (p, _) -> aux acc Module (l, p)
    | Lapply (l1, l2), Papply (p1, p2) ->
      let acc = aux acc Module (l2, p2) in
      aux acc Module (l1, p1)
    | _ -> acc
  in
  aux init kind (lid, path)

(* If a path is in D and it includes another module path within it, then that
   module path is also in D.*)
let add_all_components acc paths =
  let seq = Lid_trie.to_seq paths in
  Seq.fold_left
    (fun acc (lid, paths) ->
      Paths.fold
        (fun (kind, path) init ->
          fold_on_common_lid_and_path_segments ~init ~kind
            ~f:(fun acc kind (lid, path) -> Lid_trie.add lid (kind, path) acc)
            (lid, path))
        paths acc)
    acc seq

let debug_print fmt =
  Format.fprintf fmt "Size: %i@;%a@;%a" (Lid_trie.size !g.paths) pp !g.paths
    pp_substs !g.substs

let log_usage ?loc kind path =
  log ~title:"use" "Use %a\n%!" Logger.fmt (fun fmt ->
      Format.fprintf fmt "%s %a %a"
        (Shape.Sig_component_kind.to_string kind)
        Path.print path
        (fun fmt -> Format.pp_print_option Location.print_loc fmt)
        loc)

(* This might not be necessary if we have a clearer two-step process ?
   Currently the handling of aliases can create loops. *)
let already_used : (Path.t, unit) Hashtbl.t = Hashtbl.create 256

(** [add_path_to_discourse] adds one path from U to the Discourse, eventually
    adding the additionnal paths described by the rules for D. TODO this could
    and probably should be done lazily.

    TODO:Q what about rule D11 ? If a path is in D and it includes another module
          path within it, then that module path is also in D. Should we consider
          only [Papply] paths or all path components for addition to D ?  *)
let rec add_path_to_discourse ?(for_open = false) env discourse kind lid path =
  (* let log = log ~title:"add_path_to_discourse" in *)
  log ~title:"add_path_to_discourse" "Adding %s %a %a"
    (Shape.Sig_component_kind.to_string kind)
    Logger.fmt
    (fun fmt -> Pprintast.longident fmt lid)
    Logger.fmt
    (fun fmt -> Path.print fmt path);
  let paths = Lid_trie.add lid (kind, path) discourse.paths in
  let substs = discourse.substs in
  let paths, substs =
    match kind with
    | Module ->
      (* TODO This should probably be done lazily *)
      let md = Env.find_module_lazy path env in
      let { paths; substs } =
        match md.md_discourse_alias with
        | None -> { paths; substs }
        | Some (alias_lid, (_, alias_path)) -> begin
          try
            let path', _ = Env.find_module_by_name_lazy alias_lid.txt env in
            log ~title:"add_path_to_discourse" "Adding alias: %a %a %a"
              Logger.fmt
              (Fun.flip Pprintast.longident alias_lid.txt)
              Logger.fmt
              (Fun.flip Path.print alias_path)
              Logger.fmt
              (Fun.flip Path.print path');

            let substs =
              log ~title:"add_path_to_discourse"
                "New substitution 1' %a -> %a (%a)" Logger.fmt
                (Fun.flip Path.print path) Logger.fmt
                (Fun.flip Pprintast.longident alias_lid.txt)
                Logger.fmt
                (Fun.flip Location.print_loc alias_lid.loc);
              Path.Map.update path
                (function
                  | None -> Some (Lid_set.singleton alias_lid.txt)
                  | Some lids -> Some (Lid_set.add alias_lid.txt lids))
                substs
            in
            if Hashtbl.mem already_used path' then { paths; substs }
            else begin
              Hashtbl.add already_used path' ();
              add_used env Module alias_lid path' { paths; substs }
            end
          with Not_found -> { paths; substs }
        end
      in
      (* D5. If a module path is in U and its module description was written then
         the paths used in that description are in D *)
      (* TODO : If a path is in D and it includes another module path within it,
         then that module path is also in D. *)
      let paths = Lid_trie.union paths md.md_discourse in
      begin
        match md.md_type with
        | Mty_alias path' ->
          (* D12. If a module path m in D - note D not U - is a module alias
             with target n and another path p in D includes n within it, then
             the path obtained by substituting the m for n in p is also in D.

             We accumulate such substitution and will apply them when shortening
             a path. *)
          (* We have to follow aliases to be able to add module components to
             the discourse.

             TODO now that we have md_discourse_aliases, this might be redundant
             ? *)
          let path' = Env.normalize_module_path None env path' in
          let { paths; substs } =
            add_path_to_discourse env { paths; substs } Module lid path'
          in
          let substs =
            log ~title:"add_path_to_discourse" "New substitution 1 %a -> %a"
              Logger.fmt
              (Fun.flip Path.print path')
              Logger.fmt
              (Fun.flip Pprintast.longident lid);
            Path.Map.update path'
              (function
                | None -> Some (Lid_set.singleton lid)
                | Some lids -> Some (Lid_set.add lid lids))
              substs
          in
          (paths, substs)
        | Mty_signature s ->
          let ldot id =
            if for_open then lid else Longident.Ldot (lid, Ident.name id)
          in
          let pdot id = Path.Pdot (path, Ident.name id) in
          (* D3. If a module path is in U then all the paths of its subcomponents
             are in D *)
          List.fold_left
            (fun (paths, substs) ->
              let add kind id =
                log ~title:"add_path_to_discourse"
                  "Adding signature component %s %a (%a)"
                  (Shape.Sig_component_kind.to_string kind)
                  Logger.fmt
                  (fun fmt -> Ident.print fmt id)
                  Logger.fmt
                  (Fun.flip Path.print (pdot id));
                Lid_trie.add (ldot id) (kind, pdot id) paths
              in
              function
              | Subst.Lazy.Sig_value (id, _, _) -> (add Value id, substs)
              | Subst.Lazy.Sig_type (id, _, _, _) -> (add Type id, substs)
              | Subst.Lazy.Sig_typext (id, _, _, _) ->
                (add Extension_constructor id, substs)
              | Subst.Lazy.Sig_module (id, _, _, _, _) ->
                let md = Env.find_module_lazy (pdot id) env in
                let paths = add Module id in
                let substs =
                  match md.md_type with
                  | Mty_alias path' ->
                    let lid = ldot id in
                    log ~title:"add_path_to_discourse"
                      "New substitution 2 %a -> %a" Logger.fmt
                      (Fun.flip Path.print path')
                      Logger.fmt
                      (Fun.flip Pprintast.longident lid);
                    Path.Map.update path'
                      (function
                        | None -> Some (Lid_set.singleton lid)
                        | Some lids -> Some (Lid_set.add lid lids))
                      substs
                  | _ -> substs
                in
                (paths, substs)
              | Subst.Lazy.Sig_modtype (id, _, _) -> (add Module_type id, substs)
              | Subst.Lazy.Sig_class (id, _, _, _) -> (add Class id, substs)
              | Subst.Lazy.Sig_class_type (id, _, _, _) ->
                (add Class_type id, substs))
            (paths, substs)
            (Subst.Lazy.force_signature_once s)
        | _ -> (paths, substs)
      end
    | Module_type ->
      let mtd = Env.find_modtype_lazy path env in
      (* D8. If a module type path is in U then any paths used in its definition
         are in *)
      (* TODO : If a path is in D and it includes another module path within it,
         then that module path is also in D. *)
      (Lid_trie.union paths mtd.mtd_discourse, substs)
    | Value ->
      (* D4. If a value path is in U and its value description was written by a user -
         as opposed to being inferred - then the paths used in that description are
         in D. *)
      (* TODO : If a path is in D and it includes another module path within it,
         then that module path is also in D. *)
      let vd = Env.find_value path env in
      (Lid_trie.union paths vd.val_discourse, substs)
    | Type ->
      (* D6. If a type path is in U then any paths used in its equation or
         representation are in D. *)
      (* TODO : If a path is in D and it includes another module path within it,
         then that module path is also in D. *)
      let td = Env.find_type path env in
      (* What does it mean when such a path is just an ident that is local to
         another module ?*)
      (Lid_trie.union paths td.type_discourse, substs)
    | _ -> (paths, substs)
  in
  { paths; substs }

(** [add_used] adds all parts of a used path to the Discourse (U1, D2) *)
and add_used env kind lid path t =
  let loc = lid.Location.loc in
  let f acc kind (lid, path) =
    let () = log_usage ~loc kind path in
    try add_path_to_discourse env acc kind lid path
    with Not_found | Env.Error (Lookup_error _) -> acc
  in
  fold_on_common_lid_and_path_segments ~init:t ~kind ~f (lid.txt, path)

let add_used env kind lid path =
  if record_usages then g := add_used env kind lid path !g

let lid_and_path_of_ident ?root_lid ?root_path id =
  let lid =
    match root_lid with
    | Some lid -> Longident.Ldot (lid, Ident.name id)
    | None -> Longident.Lident (Ident.name id)
  in
  let path =
    match root_path with
    | Some path -> Path.Pdot (path, Ident.name id)
    | None -> Path.Pident id
  in
  (lid, path)

let add_subst path lid =
  log ~title:"add_path_to_discourse" "New substitution %a -> %a" Logger.fmt
    (Fun.flip Path.print path) Logger.fmt
    (Fun.flip Pprintast.longident lid);
  let substs =
    Path.Map.update path
      (function
        | None -> Some (Lid_set.singleton lid)
        | Some lids -> Some (Lid_set.add lid lids))
      !g.substs
  in
  g := { !g with substs }

(* Rule U2: All paths for definitions in the current file are in U *)
let define kind ?root_path ?root_lid id =
  if record_usages then begin
    (* let path, _ = env_lookup lid env in *)
    let lid, path = lid_and_path_of_ident ?root_path ?root_lid id in
    log ~title:"def" "Define %s %a [%a]\n%!"
      (Shape.Sig_component_kind.to_string kind)
      Logger.fmt
      (fun fmt -> Pprintast.longident fmt lid)
      Logger.fmt
      (fun fmt -> Path.print fmt path);
    let discourse = !g in
    g :=
      { discourse with paths = Lid_trie.add lid (kind, path) discourse.paths }
  end

(* Rule U3: All paths for things “defined” using include or open in the current
   file are in U. *)

let rec define_signature ?root_path ?root_lid sg =
  log ~title:"def" "Define signature";
  if record_usages then List.iter (define_component ?root_path ?root_lid) sg

and define_component ?root_path ?root_lid sig_item =
  if record_usages then
    (* let lident id = Longident.Lident (Ident.name id) in *)
    match sig_item with
    | Types.Sig_type (id, _, _, _) -> define_type ?root_path ?root_lid id
    | Types.Sig_value (id, _, _) -> define_value ?root_path ?root_lid id
    | Types.Sig_typext (_, _, _, _) -> ()
    | Types.Sig_module (id, _, md, _, _) ->
      define_module ?root_path ?root_lid md id
    | Types.Sig_modtype (id, _, _) -> define_modtype ?root_path ?root_lid id
    | Types.Sig_class (_, _, _, _) | Types.Sig_class_type (_, _, _, _) ->
      (* TODO *) ()

and define_type ?root_path ?root_lid id = define ?root_path ?root_lid Type id

and define_value ?root_path ?root_lid id = define ?root_path ?root_lid Value id

and define_module ?root_path ?root_lid (decl : Types.module_declaration) id =
  define Module ?root_path ?root_lid id;
  let root_lid, root_path = lid_and_path_of_ident ?root_path ?root_lid id in
  match decl.md_type with
  | Mty_ident path | Mty_alias path -> add_subst path root_lid
  | Mty_signature module_type ->
    define_signature ~root_path ~root_lid module_type
  | _ -> ()

and define_modtype ?root_path ?root_lid id =
  define ?root_path ?root_lid Module_type id

let define_signature_for_open env ~root_path (sg : Subst.Lazy.signature) =
  List.iter
    (fun sig_item ->
      match sig_item with
      | Subst.Lazy.Sig_type (id, _, _, _) -> define_type ~root_path id
      | Subst.Lazy.Sig_value (id, _, _) -> define_value ~root_path id
      | Subst.Lazy.Sig_typext (_, _, _, _) -> ()
      | Subst.Lazy.Sig_module (id, _, _md, _, _) ->
        log ~title:"define_signature_for_open" "Sig_module %a" Logger.fmt
          (Fun.flip Ident.print id);
        let lid, path = lid_and_path_of_ident ~root_path id in
        add_subst path lid;
        g := add_path_to_discourse env !g Module lid path
      | Subst.Lazy.Sig_modtype (id, _, _) -> define_modtype ~root_path id
      | Subst.Lazy.Sig_class (_, _, _, _)
      | Subst.Lazy.Sig_class_type (_, _, _, _) -> (* TODO *) ())
    (Subst.Lazy.force_signature_once sg)

(* TODO This should be done lazyly*)
let open_module env path =
  if record_usages then begin
    log ~title:"def" "Open module %a\n%!" Logger.fmt (fun fmt ->
        Path.print fmt path);
    try
      (* When opening we need to traverse the aliases to get the components *)
      let root_path = Env.normalize_module_path None env path in
      let md = Env.find_module_lazy root_path env in
      match md.md_type with
      | Mty_signature sg -> define_signature_for_open env ~root_path sg
      | _ -> ()
    with Not_found -> ()
  end

(* Rule U1: Any path occurring in the file is in U *)
let use_module env lid path = add_used env Module lid path
let use_modtype env lid path = add_used env Module_type lid path
let use_type env lid path = add_used env Type lid path
let use_value env lid path = add_used env Value lid path

let use_constructor env ({ Location.loc; _ } as lid)
    (constr : Types.constructor_description) =
  if record_usages then begin
    let () =
      (* When using a constructor, the modules appearing in its path should be
         added to U. TODO we might want to do that even if the constructor has
         been disambiguated. *)
      match lid.txt with
      | Longident.Ldot (lid, _) ->
        (* This find should not load additional CUs, because
           [lookup_structure_components] was called anyway by the compiler. *)
        let path, _ = Env.find_module_by_name_lazy lid env in
        use_module env { Location.txt = lid; loc } path
      | _ -> ()
    in
    (* If a constructor is in U then any paths used in its type are in D. *)
    g := { !g with paths = Lid_trie.union !g.paths constr.cstr_discourse }
  end

let use_label env ({ Location.loc; _ } as lid)
    (label : _ Types.gen_label_description) =
  if record_usages then begin
    let () =
      (* When using a label, the modules appearing in its path should be added
         to U. TODO we might want to do that even if the constructor has been
         disambiguated. *)
      match lid.txt with
      | Longident.Ldot (lid, _) ->
        (* This find should not load additional CUs, because [lookup_all_labels]
           was called anyway by the compiler. *)
        let path, _ = Env.find_module_by_name_lazy lid env in
        use_module env { Location.txt = lid; loc } path
      | _ -> ()
    in
    (* If a label is in U then any paths used in its type are in D. *)
    g := { !g with paths = Lid_trie.union !g.paths label.lbl_discourse }
  end
